module assemblies
using flecs.components.*
using flecs.game
using flecs.meta

assembly Room {
  prop width: f32 = 10
  prop depth: f32 = 10
  prop height: f32 = 5
  prop thickness: f32 = 0.25
  prop color: Rgb = {0.7, 0.7, 0.7}

  const t05 = $thickness / 2
  const PI = 3.1415926

  walls :- Frame{width:$, depth:$, height:$, thickness:$, color:$}

  ceiling {
    - Rectangle{$width - $t05, $depth - $t05}
    - Rotation3{$PI / 2}
    - Rgb{0.7, 0.7, 0.7}
    - Position3{y: $height - 0.5}  
  }
}

assembly Door {
  prop width: f32 = 2
  prop height: f32 = 3
  prop thickness: f32 = 0.4
  prop color: Rgb = {0.05, 0.2117, 0.58}
  prop stone_color: Rgb = {0.7, 0.7, 0.7}

  const PI = 3.1415926
  const step_height = 0.3

  entry {
    - Position3{y: $step_height / 2, z: $thickness / 3}
    - Box{$width * 1.2, $step_height, 2}
    - $stone_color
  }

  center {
    - Position3{y: $step_height + $height / 2 - $thickness}

    door_ {
      - Position3{z: -$thickness + 0.2}
      - Rectangle{$width - $thickness, $height - $thickness}
      - $color
    }

    rotate {
      - Position3{z: -$thickness + 0.1}
      - Rotation3{x: $PI / 2}

      frame :- Frame {
        width:$, 
        depth: $height, 
        height: $thickness, 
        color: $stone_color
        thickness: 0.3
      }
    }
  }
}

assembly House {
  prop width: f32 = 5
  prop depth: f32 = 5
  prop height: f32 = 5
  prop color: Rgb = {0.7, 0.7, 0.7}

  door {
    - Position3{x: -($width / 5.5), z: -($depth / 2) - 0.1}
    - Door{height: 3.0, stone_color: $color}
  }
  
  building {
    - Position3
    - Room{width:$, height:$, depth:$, color:$}
  }
}
